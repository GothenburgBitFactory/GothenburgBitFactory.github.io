#!/usr/bin/env perl

use strict;
use warnings;

my %data;
my $month;
if (@ARGV &&
    ! -f $ARGV[0])
{
  $month = shift @ARGV;
}
else
{
  $month = qw(Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec) [(localtime (time))[4]];
}

while (my $line = <>)
{
  next if $line !~ / ${month} /;
  next if $line !~ /File does not exist: /;
  next if $line =~ /favicon\.ico|robots\.txt/;

  chomp $line;
  $line =~ s/, referer:.+//;
  my ($file) = $line =~ /File does not exist: (.+)/;
  $data{$file}++;
}

# Truncate anything with < 2 404s.
for (keys %data)
{
  delete $data{$_} if $data{$_} == 1;
}

print $month, "\n\n";
printf ("%6d %s\n", $data{$_}, $_) for sort {$data{$b} <=> $data{$a}}keys %data;
exit 0;

